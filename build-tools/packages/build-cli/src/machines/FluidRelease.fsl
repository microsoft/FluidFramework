machine_name: "Fluid Unified Release Process";

// BEGIN SHARED INIT
Init 'success'
=> CheckShouldRunOptionalChecks;

Init 'failure'
=> Failed;

CheckShouldRunOptionalChecks 'success'
=> CheckValidReleaseGroup 'success'
=> CheckPolicy 'success'
=> CheckHasRemote 'success'
=> CheckBranchUpToDate 'success'
=> CheckNoPrereleaseDependencies 'success'
=> AskForReleaseType;

[
CheckValidReleaseGroup
CheckPolicy
CheckHasRemote
CheckBranchUpToDate
] 'failure' => Failed;

CheckNoPrereleaseDependencies 'failure'
=> DoBumpReleasedDependencies;
// END SHARED INIT


// BEGIN DoBumpReleasedDependencies
DoBumpReleasedDependencies 'success' // No dependencies to bump
=> CheckNoPrereleaseDependencies2 'success'
=> CheckShouldCommitDeps 'success'
=> PromptToPRDeps;

DoBumpReleasedDependencies 'failure' // Dependencies were bumped
=> CheckNoPrereleaseDependencies3 'failure'
=> PromptToReleaseDeps;

DoBumpReleasedDependencies
~> Failed;
// END DoBumpReleasedDependencies


// BEGIN PATCH RELEASE
AskForReleaseType 'patch'
=> DoPatchRelease 'success'
=> CheckDoesReleaseFromReleaseBranch 'success'
=> CheckOnReleaseBranch 'success'
=> CheckReleaseIsDone 'success'
=> CheckReleaseGroupIsBumpedPatch 'success'
=> CheckTypeTestPrepare 'success'
=> CheckTypeTestGenerate 'success'
=> ReleaseComplete;

CheckDoesReleaseFromReleaseBranch => 'failure'
CheckReleaseIsDone;

CheckOnReleaseBranch => 'failure'
Failed;

CheckReleaseIsDone 'failure'
=> PromptToRelease;

CheckReleaseGroupIsBumpedPatch 'failure'
=> DoReleaseGroupBump;

DoReleaseGroupBump 'success'
=> CheckShouldCommitBump 'success'
=> PromptToPRBump;

CheckShouldCommitBump 'failure'
=> PromptToCommitBump;

[
CheckTypeTestPrepare
CheckTypeTestGenerate
] 'failure'
=> PromptToRunTypeTests;
// END PATCH RELEASE

// BEGIN MINOR RELEASE
AskForReleaseType 'minor'
=> DoMinorRelease 'success'
=> CheckDoesReleaseFromReleaseBranch2 'success'
=> CheckOnReleaseBranch2 'success'
=> CheckReleaseGroupIsBumpedPatch2 'success'
=> CheckReleaseIsDone2 'success'
=> CheckTypeTestPrepare2 'success'
=> CheckTypeTestGenerate2 'success'
=> ReleaseComplete;

CheckDoesReleaseFromReleaseBranch2 'failure'
=> CheckReleaseIsDone2;

CheckReleaseGroupIsBumpedPatch2 'failure'
=> DoReleaseGroupBump;

CheckOnReleaseBranch2 'failure'
=> CheckReleaseGroupIsBumpedMinor 'success'
=> CheckReleaseBranchExists 'success'
=> CheckReleaseIsDone;

CheckReleaseBranchExists 'failure'
=> PromptToCreateReleaseBranch;

CheckReleaseIsDone2 'failure'
=> PromptToRelease;

CheckReleaseGroupIsBumpedMinor 'failure'
=> DoReleaseGroupBump;
// END MINOR RELEASE


// BEGIN MAJOR RELEASE
AskForReleaseType 'major'
=> DoMajorRelease 'success'
=> CheckDoesReleaseFromReleaseBranch3 'success'
=> CheckMainNextIntegrated 'failure'
=> CheckOnReleaseBranch3 'success'
=> Failed;

CheckDoesReleaseFromReleaseBranch3 'failure'
=> CheckReleaseIsDone3 'success'
=> PromptToRelease;

CheckOnReleaseBranch3 'failure'
=> CheckBranchName 'success'
=> PromptToIntegrateNext;

CheckBranchName 'failure'
=> Failed;

CheckMainNextIntegrated 'success'
=> CheckReleaseIsDone3 'failure'
=> PromptToRunMinorReleaseCommand;
// END MAJOR RELEASE


// visual styling
state DoMajorRelease: {
   background-color : steelblue;
   text-color       : white;
};

state DoMinorRelease: {
   background-color : steelblue;
   text-color       : white;
};

state DoPatchRelease: {
   background-color : steelblue;
   text-color       : white;
};

state DoReleaseGroupBump: {
   background-color : steelblue;
   text-color       : white;
};

state DoBumpReleasedDependencies: {
   background-color : steelblue;
   text-color       : white;
};

state DoChecks: {
   background-color : steelblue;
   text-color       : white;
};

// state PromptToCommitReleasedDepsBump: {
//   background-color : #ffdddd;
//   text-color       : black;
// };

state AskForReleaseType: {
   background-color : purple;
   text-color       : white;
};

state ReleaseComplete: {
   background-color : green;
   text-color       : black;
};
